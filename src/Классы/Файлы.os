
// Возвращает текст файла
//
//  Возвращаемое значение:
//   Строка - текст файла
//
Функция ПрочитатьФайл(Знач ПутьКФайлу, Знач Кодировка = "UTF-8") Экспорт
	
	ЧтениеТекста = Новый ЧтениеТекста(ПутьКФайлу, Кодировка);
	ТекстФайла = ЧтениеТекста.Прочитать();
	ЧтениеТекста.Закрыть();

	Возврат ТекстФайла;

КонецФункции

// Записывает текст в файл
//
// Параметры:
//   ПутьКФайлу - Строка - путь к файлу
//   ТекстФайла - Строка - текст записи
//
Процедура ЗаписатьФайл(Знач ПутьКФайлу, Знач ТекстФайла) Экспорт

	ЗаписьТекста = Новый ЗаписьТекста(ПутьКФайлу);
	ЗаписьТекста.Записать(ТекстФайла);
	ЗаписьТекста.Закрыть();

КонецПроцедуры

// Преобразует объект в json строку
//
// Параметры:
//   Объект - Число, Строка, Дата, Структура, Соответствие, Массив - объект преобразования в json
//   ПараметрыЗаписи - ПараметрыЗаписиJSON - параметры записи в json
//
//  Возвращаемое значение:
//   Строка - строка в формате json
//
Функция ОбъектВJson(Знач Объект, ПараметрыЗаписи = Неопределено) Экспорт
	
	Если ПараметрыЗаписи = Неопределено Тогда
		ПараметрыЗаписи = Новый ПараметрыЗаписиJSON(ПереносСтрокJSON.Авто, " ", ,,,,,, Истина);
	КонецЕсли;

	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку(ПараметрыЗаписи);
	ЗаписатьJSON(ЗаписьJSON, Объект);
	
	Возврат ЗаписьJSON.Закрыть();

КонецФункции

// Преобразует json в объект
//
// Параметры:
//   ТекстJSON - Строка - текст в формате json
//   ЧитатьВСтруктуру - Булево - признак чтения в структуру
//   ФорматДаты - ФорматДатыJSON - формат чтения даты (по умолчанию ФорматДатыJSON.ISO)
//
//  Возвращаемое значение:
//   Структура, Соответствие, Массив - преобразованный объект
//
Функция ОбъектИзJson(ТекстJSON, ЧитатьВСтруктуру = Ложь, ФорматДаты = Неопределено) Экспорт
		
	Если ФорматДаты = Неопределено Тогда
		ФорматДаты = ФорматДатыJSON.ISO;
	КонецЕсли;
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(ТекстJSON);
	
	Объект = ПрочитатьJSON(ЧтениеJSON,
							НЕ ЧитатьВСтруктуру,,
							ФорматДаты);
	ЧтениеJSON.Закрыть();
	Возврат Объект;
	 
КонецФункции